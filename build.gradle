import org.jsweet.transpiler.EcmaScriptComplianceLevel;

apply plugin: 'java'
apply plugin: 'maven'

group = 'org.jsweet'
version = "2.0.0-rc1"

description = """A set of examples to illustrate JSweet"""

sourceCompatibility = 1.8
targetCompatibility = 1.8

buildscript {
	boolean snapshotMode = false;
	project.ext.repoType =  snapshotMode ? 'snapshot' : 'release';
	
	println "repoType=" + project.repoType

	repositories {
		mavenCentral()
		maven { url "http://repository.jsweet.org/artifactory/libs-" + project.repoType + "-local" }
		maven { url "http://repository.jsweet.org/artifactory/plugins-" + project.repoType + "-local" }
	}
	dependencies {
		classpath('org.jsweet:jsweet-gradle-plugin:2.0.0-rc1') { //
			transitive = true }
	}
}


task wrapper(type: Wrapper) {
  description = "Generates gradlew(.bat) scripts. To update, run this after changing Gradle's version number."
  gradleVersion = "3.3"

  // Include Gradle documentation and source
  distributionUrl = "https://services.gradle.org/distributions/gradle-${gradleVersion}-all.zip"
}

repositories {
  mavenCentral()
  maven { url "http://repository.jsweet.org/artifactory/libs-" + project.repoType + "-local" }
}

configurations {
  jsweetcorestrict
}

dependencies {
	compile group: 'org.jsweet', name: 'jsweet-transpiler', version: "2.0.0-rc1"
	compile group: 'org.jsweet', name: 'jsweet-core', version: "5-20170726"
	compile group: 'org.jsweet.candies', name: 'angular', version: "1.4.0-20170726"
	compile group: 'org.jsweet.candies', name: 'angular-route', version: "1.2.0-20170726"
	compile group: 'org.jsweet.candies', name: 'es6-promise', version:"0.0.0-20170726"
	compile group: 'org.jsweet.candies', name: 'backbone', version:"1.3.0-20170726"
	compile group: 'org.jsweet.candies', name: 'knockout', version:"3.4.0-20170726"
	// Allows us to use 'marked', a Markdown to HTML converter: https://github.com/chjj/marked
	compile group: 'org.jsweet.candies', name: 'marked', version: "0.0.0-20170726"
}

compileJava {
  enabled = false
}

apply plugin: 'org.jsweet.jsweet-gradle-plugin'

jsweet {
  verbose = true
  encoding = 'UTF-8'
  sourceMap = true
  outDir = new File('target/js')
  targetVersion = EcmaScriptComplianceLevel.ES3
  includes = ['**/org/jsweet/examples/**/*.java', '**/def/**/*.java']
}
